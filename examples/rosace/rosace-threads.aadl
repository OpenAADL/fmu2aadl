package ROSACE::Threads

-- This package models threads for the ROSACE case study. It assumes a
-- one function per task mapping, it uses the timeline of Figure 11 in
-- the ROSACE paper to define the Dispatch_Offset, and table p7 to
-- define the period.

public
  with FMI;
  with ROSACE::Software;

  -----------------
  -- Va_Filter_T --
  -----------------

  thread Va_Filter_T
  features
    Va     : in  data port FMI::fmi2Real;
    Output : out data port FMI::fmi2Real;

  properties
    Dispatch_Protocol => Periodic;
    Period            => 10 ms; -- 100 Hz
  end Va_Filter_T;

  thread implementation Va_Filter_T.impl
  calls
    Call1 : { S : subprogram ROSACE::Software::Va_Filter; };

  connections
    C1 : parameter Va -> S.Va;
    C3 : parameter S.Output -> Output;
  end  Va_Filter_T.impl;

  ----------------
  -- H_Filter_T --
  ----------------

  thread H_Filter_T
  features
    H      : in  data port FMI::fmi2Real;
    Output : out data port FMI::fmi2Real;

  properties
    Dispatch_Protocol => Periodic;
    Period            => 10 ms; -- 100 Hz
  end H_Filter_T;

  thread implementation H_Filter_T.impl
  calls
    Call1 : { S : subprogram ROSACE::Software::H_Filter; };

  connections
    C1 : parameter H -> S.H;
    C3 : parameter S.Output -> Output;
  end  H_Filter_T.impl;

  -----------------
  -- Az_Filter_T --
  -----------------

  thread Az_Filter_T
  features
    Az     : in  data port FMI::fmi2Real;
    Output : out data port FMI::fmi2Real;

  properties
    Dispatch_Protocol => Periodic;
    Period            => 10 ms; -- 100 Hz
  end Az_Filter_T;

  thread implementation Az_Filter_T.impl
  calls
    Call1 : { S : subprogram ROSACE::Software::Az_Filter; };

  connections
    C1 : parameter Az -> S.Az;
    C3 : parameter S.Output -> Output;
  end  Az_Filter_T.impl;

  -----------------
  -- Vz_Filter_T --
  -----------------

  thread Vz_Filter_T
  features
    Vz     : in  data port FMI::fmi2Real;
    Output : out data port FMI::fmi2Real;

  properties
    Dispatch_Protocol => Periodic;
    Period            => 10 ms; -- 100 Hz
  end Vz_Filter_T;

  thread implementation Vz_Filter_T.impl
  calls
    Call1 : { S : subprogram ROSACE::Software::Vz_Filter; };

  connections
    C1 : parameter Vz -> S.Vz;
    C3 : parameter S.Output -> Output;
  end  Vz_Filter_T.impl;

  ----------------
  -- Q_Filter_T --
  ----------------

  thread Q_Filter_T
  features
    Q     : in  data port FMI::fmi2Real;
    Output : out data port FMI::fmi2Real;

  properties
    Dispatch_Protocol => Periodic;
    Period            => 10 ms; -- 100 Hz
  end Q_Filter_T;

  thread implementation Q_Filter_T.impl
  calls
    Call1 : { S : subprogram ROSACE::Software::Q_Filter; };

  connections
    C1 : parameter Q -> S.Q;
    C3 : parameter S.Output -> Output;
  end  Q_Filter_T.impl;

  ---------------------
  -- Altitude_Hold_T --
  ---------------------

  thread Altitude_Hold_T
  features
    H_f    : in  data port FMI::fmi2Real;
    H_c    : in  data port FMI::fmi2Real;
    Output : out data port FMI::fmi2Real;

  properties
    Dispatch_Protocol => Periodic;
    Period            => 20 ms; -- 50 Hz
  end Altitude_Hold_T;

  thread implementation Altitude_Hold_T.impl
  calls
    Call1 : { S : subprogram ROSACE::Software::Altitude_Hold; };

  connections
    C1 : parameter H_f -> S.H_f;
    C2 : parameter H_c -> S.H_c;
    C3 : parameter S.Output -> Output;
  end  Altitude_Hold_T.impl;

  ------------------
  -- Vz_control_T --
  ------------------

  thread Vz_control_T
  features
    Vz_f   : in  data port FMI::fmi2Real;
    Vz_c   : in  data port FMI::fmi2Real;
    Q_f    : in  data port FMI::fmi2Real;
    Az_f   : in  data port FMI::fmi2Real;
    Output : out data port FMI::fmi2Real;

  properties
    Dispatch_Protocol => Periodic;
    Period            => 20 ms; -- 50 Hz
  end Vz_control_T;

  thread implementation Vz_control_T.impl
  calls
    Call1 : { S : subprogram ROSACE::Software::Vz_control; };

  connections
    C1 : parameter Vz_f -> S.Vz_f;
    C2 : parameter Vz_c -> S.Vz_c;
    C3 : parameter Q_f -> S.Q_f;
    C4 : parameter Az_f -> S.Az_f;
    C5 : parameter S.Output -> Output;
  end  Vz_control_T.impl;

  ------------------
  -- Va_control_T --
  ------------------

  thread Va_control_T
  features
    Va_f   : in  data port FMI::fmi2Real;
    Vz_f   : in  data port FMI::fmi2Real;
    Q_f    : in  data port FMI::fmi2Real;
    Va_c   : in  data port FMI::fmi2Real;
    Output : out data port FMI::fmi2Real;

  properties
    Dispatch_Protocol => Periodic;
    Period            => 20 ms; -- 50 Hz
  end Va_control_T;

  thread implementation Va_control_T.impl
  calls
    Call1 : { S : subprogram ROSACE::Software::Va_control; };

  connections
    C1 : parameter Va_f -> S.Va_f;
    C2 : parameter Vz_f -> S.Vz_f;
    C3 : parameter Q_f -> S.Q_f;
    C4 : parameter Va_c -> S.Va_c;
    C5 : parameter S.Output -> Output;
  end  Va_control_T.impl;

  ------------------
  -- ROSACE_Log_T --
  ------------------

  thread ROSACE_Log_T
  features
    T          : in data port FMI::fmi2Real;
    Va         : in data port FMI::fmi2Real;
    Az         : in data port FMI::fmi2Real;
    Q          : in data port FMI::fmi2Real;
    Vz         : in data port FMI::fmi2Real;
    H          : in data port FMI::fmi2Real;
    Delta_th_c : in data port FMI::fmi2Real;
    Delta_E_C  : in data port FMI::fmi2Real;

  properties
    Dispatch_Protocol => Periodic;
    Period            => 20 ms; -- 50 Hz
  end ROSACE_Log_T;

  thread implementation ROSACE_Log_T.impl
  calls
    Call1 : { S : subprogram ROSACE::Software::ROSACE_Log; };

  connections
    C1 : parameter T -> S.T;
    C2 : parameter Va -> S.Va;
    C3 : parameter Az -> S.Az;
    C4 : parameter Q -> S.Q;
    C5 : parameter Vz -> S.Vz;
    C6 : parameter H -> S.H;
    C7 : parameter Delta_Th_C -> S.Delta_Th_C;
    C8 : parameter Delta_E_C -> S.Delta_E_C;

  end  ROSACE_Log_T.impl;

end ROSACE::Threads;